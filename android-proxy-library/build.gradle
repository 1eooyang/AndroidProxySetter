apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

//    Scheme for version codes: ##@@$$$ , using:
//    ##   - the first two digits for the API Level,
//    @@   - the second and third digits for the minimum and maximum screen size
//           (1 - 4 indicating each of the four sizes) or to denote the texture formats
//    $$$  - the last three digits for the app version.

android {

    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 21
        versionCode 800400
        versionName "4.0"
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
                exclude '**/excluded_from_build/**'
            }
        }
    }
}

dependencies {

    compile 'com.jakewharton.timber:timber:2.5.0'
}


version = "4.0.0"
//version = '2.9.0-SNAPSHOT'
group = "be.shouldit"

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

if (isReleaseVersion) {

    println 'Building RELEASE'
    ext.sonatypeRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

} else {

    println 'Building SNAPSHOT'
    ext.sonatypeRepo = "https://oss.sonatype.org/content/repositories/snapshots/"

}

ext.sonatypeUsername = has("sonatypeUsername") ? sonatypeUsername : ""
ext.sonatypePassword = has("sonatypePassword") ? sonatypePassword : ""

configurations {

    archives {

        extendsFrom configurations.default

    }

}

repositories {

    mavenCentral()

}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task publishLocal(type: Upload) {

    configuration = configurations.archives
    repositories.mavenDeployer {
        repository(url: "file://$buildDir/repo")
    }

}

uploadArchives {

    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: project.ext.sonatypeRepo) {
            authentication(userName: project.ext.sonatypeUsername,
                    password: project.ext.sonatypePassword)
        }

        pom.project {
            name 'Android Proxy Library'
            packaging 'aar'
            description 'Proxy utility library for Android applications'
            url 'https://github.com/shouldit/android-proxy'

            scm {
                url 'scm:git@github.com:shouldit/android-proxy.git'
                connection 'scm:git@github.com:shouldit/android-proxy.git'
                developerConnection 'scm:git@github.com:shouldit/android-proxy.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'lechuckcaptain'
                    name 'Marco Pagliari'
                    email 'lechuckcaptain@gmail.com'
                }
            }
        }
    }
}
